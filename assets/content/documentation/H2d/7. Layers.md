# Layers

The Layers module provides additional functionality to the Sprite class, allowing for the ordering of Sprites. If you've ever used Flash, OpenFL or Starling, then you can envisage Layers to be a little like the MovieClip object from those libraries.

Essentially, when working with a display scene, each visual element you add to the scene is layered one on top of the other.  This is known as the `View Stack`.  Oftimes, you will want to manage this stack, so that you can move sprites on to different layers.  For instance, at one time, Sprite A might be on top of Sprite B, but at another, you might want Sprite B to be on top.  The Layers module helps you to do this.

```haxe
class Main extends hxd.App {
  var bmp1 : h2d.Bitmap;
  var bmp2 : h2d.Bitmap;
  var curState : String;
  var prevState : String;
  override function init() {
      // allocate two Textures; one red and one blue and create 100x100 Tiles from then
      var tile1 = h2d.Tile.fromColor(0xFF0000, 100, 100);
      var tile2 = h2d.Tile.fromColor(0x0000FF, 100, 100);
      prevState = curState = "left";
      // create Bitmap sprites, which will display the tile
      // and will be added to our 2D scene (s2d)
      bmp1 = new h2d.Bitmap(tile1, s2d);
      bmp2 = new h2d.Bitmap(tile2, s2d);
      // modify the display position of the Bitmap sprite
      bmp1.x = s2d.width * 0.5 - 25;
      bmp2.x = s2d.width * 0.5 + 25;
      bmp1.y = bmp2.y = s2d.height * 0.5;
  }
  // on each frame
  override function update(dt:Float) {
    // check the x position of the mouse and update the current state
    // based on which half of the screen the mouse is in.
    if (s2d.mouseX > s2d.width / 2) {
      curState = "right";
    } else {
      curState = "left";
    }
    // if the current state has changed
    if (curState != prevState) {
      switch (curState) {
        case "left":
          // if state is "left", place bmp1 on top
          s2d.removeChild(bmp1);
          s2d.addChildAt(bmp1, 0);
        case "right":
          // if state is "right", place bmp2 on top
          s2d.removeChild(bmp2);
          s2d.addChildAt(bmp2, 0);
      }
      prevState = curState;
    }
  }
  static function main() {
      new Main();
  }
}
```

Note that the main `Scene` stack extends the `Layers` module.